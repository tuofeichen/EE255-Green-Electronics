* Copyright 2013 Avago Technologies Limited. All Right Reserved
*
* ACPL-P346 and ACPL-W346 Spice Macromodel
* Rev. A 
* 04/13
* LSP
* 
* Macro model performance matches the typical datasheet specifications 
*     - SPICE Model is verified by Orcad PSpice at Ta=25^C. 
*     - Macro model performance matches the typical datasheet specifications 
*     - Worst case performance are not modeled. 
*
* Macromodels provided by Avago Technologies are not warranted
* as fully representing all of the specification and operating 
* characteristics of the product.
*
* Macromodels are useful for evaluating product performance but they
* cannot model exact device performance under all condition, nor are
* they intented to replace breadboarding for final verification.
*
**********************************************************************
* 		  Pin configuration
*                 1  2  3  4   5    6	
*                 |  |  |  |   |    |                             
.subckt acpl-p346 AN NC CA VEE VOUT VCC
XX4 AN CA VE N005 DVDD opto
XX23 DVDD VCC N002 VE N001 comparator
R4 N002 VE 6.35k
I3 VE N002 1.5m
R5 N002 N001 25k
R7 5V VCC 1.93k
E5 DVDD VE 5V VE 1
V2 5V VE 5
R10 VEE VE 0.0001
V4 NVDD VEE 5
V5 VCC PVSS 5
M9 VOUT N004 VCC VCC PMOS1 W=1052u L=0.6u
M3 VOUT LGNMOS VEE VEE NMOS1 W=1500u L=0.6u
E4 VCC N003 HSON_B VE 1
M5 N004 N003 VCC VCC PMOS1 W=200u L=0.6u
M6 N006 N003 PVSS PVSS NMOS1 W=10u L=0.6u
R3 N004 N006 4.5k
M7 N009 N008 NVDD NVDD PMOS1 W=20u L=0.6u
M10 LGNMOS N008 VEE VEE NMOS1 W=100u L=0.6u
R14 N009 LGNMOS 4.5k
E2 N008 VEE LSON_B VE 1
XX1 UVON N005 N007 DVDD DRV VE DVDD and4
XX3 DRV DRV_B VE DVDD inv
XX6 LSON LSON_B VE DVDD inv
XX7 DRV_B DVDD N007 LSON VE DVDD and3
XX2 VE DRV_B HSON_B VE DVDD or2
XX5 VE N007 VE DVDD inv
XX8 N001 UVON VE DVDD inv
.ends acpl-p346 

.subckt opto an ca gnd2 vout vcc2
E1 N008 N009 N006 N007 {CTR}
XX1 an ca N007 N006 vbu
XX2 N008 N009 N002 N003 pdnoc
E2 N001 gnd2 vcc2 gnd2 1
I1 N002 N003 1.2u
XX3 N002 N002 gnd2 N001 inv
XX4 N004 N005 gnd2 vcc2 inv
XX6 N005 vout gnd2 vcc2 inv
C1 N005 gnd2 2p
XX5 N003 N004 N002 tia
.MODEL PMOS1 PMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=-8.20E-1 RDS=1.0000E6 TOX=1.24E-8 CGSO=2.01E-10 CGDO=2.01E-10 CBD=0
+ RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=610
.MODEL NMOS1 NMOS LEVEL=3 L=5.0000E-7 W=2E-6 RS=10.000E-3 RD=10.000E-3
+ VTO=7.55E-1 RDS=1.0000E6 TOX=1.25E-8 CGSO=1.83E-10 CGDO=1.83E-10 
+ CBD=1.0000E-12 RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=650
.param CTR=0.08
.ends opto

.subckt comparator VDD VINN VINP VSS VO
G1 N005 N001 VINP VINN 0.1
D1 N001 N003 D
D2 N003 N005 D
D3 N004 N001 D
D4 N005 N004 D
XX1 N001 N002 VSS VDD inv
XX2 N002 VO VSS VDD inv
XX3 N005 N005 VSS VDD inv
.ends comparator

.subckt and4 A B C D Y VSS VDD
M1 N001 A N002 N002 NMOS1 W=2u L=0.6u
M2 N001 A VDD VDD PMOS1 W=5u L=0.6u
M3 N002 B N003 N003 NMOS1 W=2u L=0.6u
M4 N003 C N004 N004 NMOS1 W=2u L=0.6u
M5 N001 B VDD VDD PMOS1 W=5u L=0.6u
M6 N001 C VDD VDD PMOS1 W=5u L=0.6u
M7 N001 D VDD VDD PMOS1 W=5u L=0.6u
M8 N004 D VSS VSS NMOS1 W=2u L=0.6u
M9 Y N001 VDD VDD PMOS1 W=5u L=0.6u
M10 Y N001 VSS VSS NMOS1 W=2u L=0.6u
.MODEL PMOS1 PMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=-8.20E-1 RDS=1.0000E6 TOX=1.24E-8 CGSO=2.01E-10 CGDO=2.01E-10 CBD=0
+ RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=610
.MODEL NMOS1 NMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=7.55E-1 RDS=1.0000E6 TOX=1.25E-8 CGSO=1.83E-10 CGDO=1.83E-10 
+ CBD=1.0000E-12 RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=650
.ends and4

.subckt inv A Y VSS VDD
M1 Y A VSS VSS NMOS1 W=280u L=0.6u
M2 Y A VDD VDD PMOS1 W=260u L=0.6u
.MODEL PMOS1 PMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=-8.20E-1 RDS=1.0000E6 TOX=1.24E-8 CGSO=2.01E-10 CGDO=2.01E-10 CBD=0
+ RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=610
.MODEL NMOS1 NMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=7.55E-1 RDS=1.0000E6 TOX=1.25E-8 CGSO=1.83E-10 CGDO=1.83E-10 
+ CBD=1.0000E-12 RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=650
.ends inv

.subckt and3 A B C Y VSS VDD
M1 N001 A N002 N002 NMOS1 W=2u L=0.6u
M2 N001 A VDD VDD PMOS1 W=5u L=0.6u
M3 N002 B N003 N003 NMOS1 W=50u L=0.6u
M4 N003 C VSS VSS NMOS1 W=2u L=0.6u
M5 N001 B VDD VDD PMOS1 W=2u L=0.6u
M6 N001 C VDD VDD PMOS1 W=5u L=0.6u
M7 Y N001 VDD VDD PMOS1 W=5u L=0.6u
M8 Y N001 VSS VSS NMOS1 W=2u L=0.6u
.MODEL PMOS1 PMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=-8.20E-1 RDS=1.0000E6 TOX=1.24E-8 CGSO=2.01E-10 CGDO=2.01E-10 CBD=0
+ RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=610
.MODEL NMOS1 NMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=7.55E-1 RDS=1.0000E6 TOX=1.25E-8 CGSO=1.83E-10 CGDO=1.83E-10 
+ CBD=1.0000E-12 RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=650
.ends and3

.subckt or2 A B Y VSS VDD
M1 N001 B VSS VSS NMOS1 W=50u L=0.6u
M2 N002 A VDD VDD PMOS1 W=100u L=0.6u
M3 N001 A VSS VSS NMOS1 W=2u L=0.6u
M5 N001 B N002 N002 PMOS1 W=5u L=0.6u
M4 Y N001 VDD VDD PMOS1 W=5u L=0.6u
M6 Y N001 VSS VSS NMOS1 W=2u L=0.6u
.MODEL PMOS1 PMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=-8.20E-1 RDS=1.0000E6 TOX=1.24E-8 CGSO=2.01E-10 CGDO=2.01E-10 CBD=0
+ RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=610
.MODEL NMOS1 NMOS LEVEL=3 RS=10.000E-3 RD=10.000E-3
+ VTO=7.55E-1 RDS=1.0000E6 TOX=1.25E-8 CGSO=1.83E-10 CGDO=1.83E-10 
+ CBD=1.0000E-12 RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=650
.ends or2

.subckt vbu AN CA LOPN LOPP
RSERIES AN 5 5
DELECT 5 CA VBUNOR
ELED 6 LOPN 5 CA 1
DOPTIC 6 8 VBUNORC
FPHOTO LOPN 3 VSENSE 1
VSENSE 8 LOPN 0
RL 3 LOPN 0.1
EOUT LOPP LOPN 3 LOPN 60
VSIM LOPN CA 0
Rnl 6 N001 5k
Vnl N002 LOPN 0
Fnl 3 LOPN Vnl 1
Dsw N001 N002 DSW
.model DSW D Is=1e-4
.model VBUNOR D IS=330E-21 N=1.53 XTI=3 EG=1.52 BV=10.38 IBV=100u
+  CJO=80p VJ=.75 M=.3333 FC=.5 TT=13n
.model VBUNORC D IS=330E-21 N=1.53 XTI=3 EG=1.52 BV=10.38 IBV=100u
+  CJO=0 VJ=.75 M=.3333 FC=.5 TT=0
.ends vbu

.subckt pdnoc LOPP LOPN AN CA
D1 AN CA PDC
G1 CA AN LOPP LOPN 0.0020
V1 LOPN AN 0
.model PDC D RS=1.0E-3 CJO=0p M=0.333 VJ=0.75 ISR=100.0E-12 BV=100 TT=5E-9
.ends pdnoc

.subckt tia in out agnd
G2 agnd out agnd in 0.1
R1 N001 in 20k
C1 out in 0.5p
D1 out in D
R2 out N001 20k
C2 N002 agnd 15p
R3 N002 N001 25k
.ends tia

.model D D
.MODEL PMOS1 PMOS LEVEL=3 RS=7.4E-1 RD=7.4E-1
+ VTO=-3.20E-1 RDS=1.0000E6 TOX=24.05E-10 CGSO=2.01E-12 CGDO=2.01E-12 CBD=0 
+ RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=600
.MODEL NMOS1 NMOS LEVEL=3 RS=12.4E-2 RD=12.4E-2
+ VTO=7.55E-1 RDS=1.0000E6 TOX=79.0825E-10 CGSO=0.53E-10 CGDO=0.53E-10 CBD=0 
+ RG=5 RB=1.0000E-3 GAMMA=0 KAPPA=0 UO=600