*Dec 10, 2012
*ECN S12-2950, Rev. B
*This document is intended as a SPICE modeling guideline and does not
*constitute a commercial product datasheet. Designers should refer to the
*appropriate datasheet of the same number for guaranteed specification
*limits.
.SUBCKT Si7430DP D G S
M1  3  GX S S NMOS W=3663407u L=0.25u  
M2  S  GX S D PMOS W=3663407u L=0.50u  
RG  G  GX     0.85
R1  D  3      RTEMP 29E-3
CGS GX S      1400E-12
DBD S  D      DBD
**************************************************************** 
.MODEL  NMOS        NMOS ( LEVEL  = 3           TOX    = 10E-8
+ RS     = 6E-3            RD     = 0           NSUB   = 2.1E17   
+ kp     = 1E-5            UO     = 650             
+ VMAX   = 0               XJ     = 5E-7        KAPPA  = 1E-2
+ ETA    = 1E-4            TPG    = 1  
+ IS     = 0               LD     = 0                             
+ CGSO   = 0               CGDO   = 0           CGBO   = 0 
+ TLEV   = 1               BEX    = -1.5        TCV    = 6.5E-3
+ NFS    = 0.8E12          DELTA  = 0.1)
**************************************************************** 
.MODEL  PMOS        PMOS ( LEVEL  = 3           TOX    = 10E-8
+NSUB    = 2E15            TPG    = -1)   
**************************************************************** 
.MODEL DBD D (CJO=1500E-12 VJ=0.38 M=0.50
+FC=0.1 IS=1E-12 TT=6E-8 N=1 BV=102)
**************************************************************** 
.MODEL RTEMP R (TC1=6E-3 TC2=5.5E-6)
**************************************************************** 
.ENDS
 
